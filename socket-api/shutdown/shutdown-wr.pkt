// Borrowed from https://github.com/google/packetdrill
// This test checks for the behavior of the sequence
// shutdown(SHUT_WR), write(), close()
--tolerance_usecs=100000

// Establish the connection
0.000 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
0.000 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
0.000 bind(3, ..., ...) = 0
0.000 listen(3, 1) = 0

0.100 < S 0:0(0) win 65535 <mss 1460,sackOK,nop,nop,nop,wscale 7>
0.100 > S. 0:0(0) ack 1 <...>
0.200 < . 1:1(0) ack 1 win 65535
0.200 accept(3, ..., ...) = 4

0.210 shutdown(4, SHUT_WR) = 0
0.210 write(4, ..., 1000) = -1 EPIPE (Broken pipe)
0.210 > F. 1:1(0) ack 1
0.210 < . 1:1(0) ack 2 win 65535

0.220 close(4) = 0

0.230 < F. 1:1(0) ack 2 win 65535
0.230 > . 2:2(0) ack 2
