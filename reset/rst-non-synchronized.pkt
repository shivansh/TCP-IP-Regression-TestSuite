// Test for checking whether we move to passive open state
// when a RST is received from non-synchronized state.
--tolerance_usecs=100000

// Create a listening TCP socket
0.000 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
0.000 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
0.000 bind(3, ..., ...) = 0
0.000 listen(3, 1) = 0

0.100 < S  0:0(0) win 65535 <mss 1460,sackOK,nop,nop,nop,wscale 7>
0.100 > S. 0:0(0) ack 1 <...>
0.100 < R  1:1(0) win 65535

// Check if we revert back to passive open state
// by establishing a connection when a valid SYN arrives.
0.110 < S  0:0(0) win 65535 <mss 1460,sackOK,nop,nop,nop,wscale 7>
0.110 > S. 0:0(0) ack 1 win 65535 <...>
0.110 < .  1:1(0) ack 1 win 65535
0.110 accept(3, ..., ...) = 4

// Our application writes 1000 bytes
0.120 write(4, ..., 1000) = 1000
0.120 > P. 1:1001(1000) ack 1

// Client sends one ACK
0.130 < . 1:1(0) ack 1001 win 65535
